MODULE JULIANMOD
  ! ** AUTHOR: DANG HUU CHUNG
  ! ** DATE  : 2010-06-26
  USE GLOBAL  

  IMPLICIT NONE
   ! ** JULIAN TIME
    INTEGER(IK4),PRIVATE  :: DA0             !BASE DAY
    INTEGER(IK4),PRIVATE  :: MN0             !BASE MONTH
    INTEGER(IK4),PRIVATE  :: YR0             !BASE YEAR
    INTEGER(IK4),PRIVATE  :: HH0             !BASE HH
    INTEGER(IK4),PRIVATE  :: MM0             !BASE MM
    INTEGER(IK4),PRIVATE  :: SS0             !BASE SS 
    REAL(8),   PRIVATE  :: HR0
  
  CONTAINS

  FUNCTION JULIDAY(MM,ID,IYYY) RESULT(JULDAY)
    ! ** FROM A BOOK OF NUMERICAL METHODS
    INTEGER(IK4) :: JULDAY,ID,IYYY,MM,IGREG
    PARAMETER (IGREG=15+31*(10+12*1582))
    INTEGER(IK4) :: JA,JM,JY
    JY=IYYY
    IF( JY == 0) STOP 'JULDAY: THERE IS NO YEAR ZERO'
    IF( JY < 0) JY=JY+1
    IF( MM > 2 )THEN
      JM=MM+1
    ELSE
      JY=JY-1
      JM=MM+13
    ENDIF
    JULDAY=365*JY+INT(0.25D0*JY+2000D0)+INT(30.6001D0*JM)+ID+1718995
    IF( ID+31*(MM+12*IYYY).GE.IGREG )THEN
      JA=INT(0.01D0*JY)
      JULDAY=JULDAY+2-JA+INT(0.25D0*JA)
    ENDIF
    RETURN
  END FUNCTION

  SUBROUTINE CALDAT(JULIAN,MM,ID,IYYY)
    ! ** FROM A BOOK OF NUMERICAL METHODS
    INTEGER(IK4) :: ID,IYYY,JULIAN,MM,IGREG
    PARAMETER (IGREG=2299161)
    INTEGER(IK4) :: JA,JALPHA,JB,JC,JD,JE
    IF( JULIAN.GE.IGREG )THEN
      JALPHA=INT(((JULIAN-1867216)-0.25D0)/36524.25D0)
      JA=JULIAN+1+JALPHA-INT(0.25D0*JALPHA)
    ELSEIF( JULIAN < 0 )THEN
      JA=JULIAN+36525*(1-JULIAN/36525)
    ELSE
      JA=JULIAN
    ENDIF
    JB=JA+1524
    JC=INT(6680.0D0+((JB-2439870)-122.1D0)/365.25D0)
    JD=365*JC+INT(0.25D0*JC)
    JE=INT((JB-JD)/30.6001D0)
    ID=JB-JD-INT(30.6001D0*JE)
    MM=JE-1
    IF( MM > 12 )MM=MM-12
    IYYY=JC-4715
    IF( MM > 2 )IYYY=IYYY-1
    IF( IYYY <= 0)IYYY=IYYY-1
    IF( JULIAN < 0 ) IYYY=IYYY-100*(1-JULIAN/36525)
    RETURN
  END SUBROUTINE

   SUBROUTINE DATEPRO(STR,YR,MN,DD)
   
     CHARACTER(*),INTENT(IN) :: STR
     CHARACTER(LEN=LEN_TRIM(STR)) :: SS
     INTEGER(IK4),INTENT(OUT) :: DD,MN,YR
     INTEGER(IK4) :: M,NL
     
     SS = STR
     NL=LEN_TRIM(SS)
     DO M=1,NL
       IF( SS(M:M) == '-' .OR. SS(M:M) == ':' .OR. SS(M:M) == '/' )THEN
         SS(M:M) =''
       ENDIF
     ENDDO
     READ(SS,*) YR,MN,DD
     
   END SUBROUTINE
 
  SUBROUTINE TOGREGOR(SDATETIME,DAYTIME)
  
    REAL(8),INTENT(IN) :: DAYTIME
    CHARACTER(*),INTENT(OUT) :: SDATETIME
    INTEGER(IK4) :: JD0,JULIAN,MN,DD,YR 

    CALL DATEPRO(BASETIME,HH0,MM0,SS0) 
    CALL DATEPRO(BASEDATE,YR0,MN0,DA0) 

    HR0 = HH0+MM0/60._8+SS0/3600._8
    JD0 = JULIDAY(MN0,DA0,YR0)         
   
    JULIAN = IDINT(DAYTIME+JD0)
    CALL CALDAT(JULIAN,MN,DD,YR)

    WRITE(SDATETIME,'(I4,I2.2,I2.2)') YR,MN,DD
  
  END SUBROUTINE
 
END MODULE
