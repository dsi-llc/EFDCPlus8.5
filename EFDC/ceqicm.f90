SUBROUTINE CEQICM

  ! **  SUBROUTINE FOR INTERFACING CE-QUAL-ICM MODEL

  ! CHANGE RECORD

  USE GLOBAL
  
  IMPLICIT NONE
  
  INTEGER :: NSKIP,IDMPCL,JDMPCL,M,IDUM,JDUM,MDUM,NS,NCTL,NWR,NMD,K,KICM,L,LM
  INTEGER :: NFICMS,LTMP,NMID,NQSTMP,LE,LN,MM
  
  REAL    :: TIME,SVPT,TIMMID,TAVGTMP,TMPVAL

  REAL,SAVE,ALLOCATABLE,DIMENSION(:)    :: QINRCA
  REAL,SAVE,ALLOCATABLE,DIMENSION(:)    :: TMPICMF
  REAL,SAVE,ALLOCATABLE,DIMENSION(:,:)  :: QINTFL
  
  INTEGER,SAVE,ALLOCATABLE,DIMENSION(:) :: LFEFDC
  INTEGER,SAVE,ALLOCATABLE,DIMENSION(:) :: IDRICM

  IF(  .NOT. ALLOCATED(QINRCA) )THEN
    ALLOCATE(QINRCA(NQSIJM))
    ALLOCATE(QINTFL(NQINFLM,KCM))
    ALLOCATE(TMPICMF(2*LCM*KCM))
    ALLOCATE(IDRICM(2*LCM*KCM))
    ALLOCATE(LFEFDC(2*LCM*KCM))
    QINRCA=0.
    QINTFL=0.
    TMPICMF=0.
    IDRICM=0
    LFEFDC=0
  ENDIF

  ! **  READ CONTROL FILES AND WRITE TIME INVARIANT FILES ON FIRST ENTRY
  IF( JSWASP == 0 ) GOTO 1000
  IF( ISDYNSTP == 0 )THEN
    TIME = (DT*FLOAT(N)+TCON*TBEGIN)/86400.
  ELSE
    TIME = TIMESEC/86400.
  ENDIF
  !
  ! **  READ MAIN CONTROL FILE
  !
  WRITE(*,'(A)')'READING EFDC.ICM'
  OPEN(1,FILE='efdc.icm',STATUS='UNKNOWN')
  DO NSKIP=1,9
    READ(1,100)
  ENDDO
  READ(1,*)ISDICM,IAUXICM,ISTICM,IDMPCL,JDMPCL,NCICM,NFICM
  CLOSE(1)

  ! **  READ CELL MAPPING FILE
  WRITE(*,'(A)')'READING EFDC_C_ICM.INP'
  OPEN(1,FILE='efdc_c_icm.inp',STATUS='UNKNOWN')
  DO NSKIP=1,7
    READ(1,100)
  ENDDO
  DO M=1,NCICM
    READ(1,*)IDUM,JDUM,LCEFDC(M),KCEFDC(M),MDUM
  ENDDO
  CLOSE(1)

  ! **  READ FLOW MAPPING FILE
  WRITE(*,'(A)')'READING EFDC_F_ICM.INP'
  OPEN(1,FILE='efdc_f_icm.inp',STATUS='UNKNOWN')
  DO NSKIP=1,8
    READ(1,100)
  ENDDO
  DO M=1,NFICM
    READ(1,*)IDUM,JDUM,LFEFDC(M),KFEFDC(M),MDUM,IDRICM(M)
  ENDDO
  CLOSE(1)

  ! **  WRITE I,J INDICES DEFINING FLOWS BETWEEN ARBITARY CELLS
  ! **  (POSTIVE FLOW DIRECTION DEFINED FROM FIRST TO SECOND I,J PAIR)
  IF( IAUXICM >= 1 )THEN
    OPEN(1,FILE=OUTDIR//'flwmap.inp',STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    OPEN(1,FILE=OUTDIR//'flwmap.inp',STATUS='UNKNOWN')
    NINTFL=0
    IF( NQSIJ > 0 )THEN
      DO NS=1,NQSIJ
        NINTFL=NINTFL+1
        WRITE(1,101)IDMPCL,JDMPCL,IQS(NS),JQS(NS)
      ENDDO
    ENDIF
    IF( NQCTL > 0 )THEN
      DO NCTL=1,NQCTL
        NINTFL=NINTFL+1
        IF( IQCTLD(NCTL) > 0 )THEN
          WRITE(1,101) IQCTLU(NCTL),JQCTLU(NCTL), &
              IQCTLD(NCTL),JQCTLD(NCTL)
        ELSE
          WRITE(1,101)IQCTLU(NCTL),JQCTLU(NCTL), &
              IDMPCL,JDMPCL
        ENDIF
      ENDDO
    ENDIF
    IF( NQWR > 0 )THEN
      DO NWR=1,NQWR
        NINTFL=NINTFL+1
        WRITE(1,101)IQWRU(NWR),JQWRU(NWR),IQWRD(NWR),JQWRD(NWR)
      ENDDO
    ENDIF
    IF( MDCHH > 0 )THEN
      DO NMD=1,MDCHH
        IF( IMDCHU(NMD) > 1 )THEN
          NINTFL=NINTFL+1
         WRITE(1,101)IMDCHU(NMD),JMDCHU(NMD),IMDCHH(NMD),JMDCHH(NMD)
        ENDIF
      ENDDO
      DO NMD=1,MDCHH
        IF( IMDCHV(NMD) > 1 )THEN
          NINTFL=NINTFL+1
         WRITE(1,101)IMDCHV(NMD),JMDCHV(NMD),IMDCHH(NMD),JMDCHH(NMD)
        ENDIF
      ENDDO
    ENDIF
    CLOSE(1)
  ENDIF

  ! **  WRITE EXTERNAL INFLOW LOCATIONS
  OPEN(1,FILE=OUTDIR//'INFLOWIJ.DAT',STATUS='UNKNOWN')
  CLOSE(1,STATUS='DELETE')
  OPEN(1,FILE=OUTDIR//'INFLOWIJ.DAT',STATUS='UNKNOWN')
  WRITE(1,110)
  WRITE(1,111)
  IF( NQSIJ >= 1 )THEN
    DO NS=1,NQSIJ
      WRITE(1,112)NS,IQS(NS),JQS(NS)
    ENDDO
  ENDIF
  CLOSE(1)

  ! **  INITIALIZE INTERFACE FILES
  OPEN(1,FILE=OUTDIR//'efdchyd.inp',FORM='UNFORMATTED',STATUS='UNKNOWN')
  CLOSE(1,STATUS='DELETE')
  OPEN(1,FILE=OUTDIR//'efdchyd.inp',FORM='UNFORMATTED',STATUS='UNKNOWN')
  IF( ISDICM == 1 )THEN
    OPEN(2,FILE=OUTDIR//'EFDCHYD.ASC',STATUS='UNKNOWN')
    CLOSE(2,STATUS='DELETE')
    OPEN(2,FILE=OUTDIR//'EFDCHYD.ASC',STATUS='UNKNOWN')
  ENDIF

  ! **  WRITE SIGMA GRID FRACTIONAL LAYER THICKNESS (SURFACE DOWN FOR ICM)
  WRITE(1) (DZC(L,K),K=KC,1,-1)
  IF( ISDICM == 1 )THEN
    WRITE(2,2001)TIME
    WRITE(2,2002)
    DO K=KC,1,-1
      KICM=KC-K+1
      WRITE(2,293)KICM,DZC(L,K),K
    ENDDO
  ENDIF
  !
  ! **  WRITE CELL HORIZONTAL SURFACE AREAS (LOOP OVER SURFACE LAYER CELLS
  !
  WRITE(1) (DXYP(L),L=2,LA)
  IF( ISDICM == 1 )THEN
    WRITE(2,2003)
    DO L=2,LA
      LM=LWC(L)
      WRITE(2,293)LM,DXYP(L),L
    ENDDO
  ENDIF
  !
  ! **  WRITE CELL HORIZONTAL DEPTH AVERAGE FLOW FACE AREAS
  !
  NFICMS=NFICM/KC
  DO M=1,NFICMS
    L=LFEFDC(M)
    TMPICMF(M)=0.0  ! *** DS-INTL SINGLE LINE
    IF( IDRICM(M) == 1 ) TMPICMF(M)=DYU(L)*HMU(L)
    IF( IDRICM(M) == 2 ) TMPICMF(M)=DXV(L)*HMV(L)
  ENDDO
  WRITE(1) (TMPICMF(M),M=1,NFICMS)
  IF( ISDICM == 1 )THEN
    WRITE(2,2004)
    DO M=1,NFICMS
      WRITE(2,293)M,TMPICMF(M),IDRICM(M),LFEFDC(M)
    ENDDO
  ENDIF
  !
  ! **  WRITE CELL HORIZONTAL X DIRECTION LENGTHS
  !
  WRITE(1) (DXP(L),L=2,LA)
  IF( ISDICM == 1 )THEN
    WRITE(2,2005)
    DO L=2,LA
      LM=LWC(L)
      WRITE(2,293)LM,DXP(L),L
    ENDDO
  ENDIF
  !
  ! **  WRITE CELL HORIZONTAL Y DIRECTION LENGTHS
  !
  WRITE(1) (DYP(L),L=2,LA)
  IF( ISDICM == 1 )THEN
    WRITE(2,2006)
    DO L=2,LA
      LM=LWC(L)
      WRITE(2,293)LM,DYP(L),L
    ENDDO
  ENDIF
  !
  ! **  WRITE CELL HORIZONTAL DEPTH AVERAGE INITIAL VOLUMES
  !
  DO L=2,LA
    TVAR3C(LWC(L))=DXYP(L)*H2WQ(L)
    HTMP(L)=H2WQ(L)
  ENDDO
  LTMP=LA-1
  WRITE(1) (TVAR3C(L),L=1,LTMP)
  IF( ISDICM == 1 )THEN
    WRITE(2,2007)
    DO L=2,LA
      LM=LWC(L)
      WRITE(2,293)LM,TVAR3C(LM),L
    ENDDO
  ENDIF
  CLOSE(1)
  IF( ISDICM == 1 ) CLOSE(2)
  !
  ! **  INITIALIZE OTHER FILES TO RECEIVE TIME VARYING DATA
  !
  IF( IAUXICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'INFLOW.DAT',STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    OPEN(1,FILE=OUTDIR//'hydrlgy.inp',FORM='UNFORMATTED' &
           ,STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    IF( ISDICM == 1 )THEN
      OPEN(1,FILE=OUTDIR//'HYDRLGY.ASC',STATUS='UNKNOWN')
      CLOSE(1,STATUS='DELETE')
    ENDIF
    OPEN(1,FILE=OUTDIR//'intflw.inp',FORM='UNFORMATTED' &
           ,STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    IF( ISDICM == 1 )THEN
      OPEN(1,FILE=OUTDIR//'INTFLW.ASC',STATUS='UNKNOWN')
      CLOSE(1,STATUS='DELETE')
    ENDIF
  ENDIF
  IF( ISTICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'salicm.inp',FORM='UNFORMATTED' &
           ,STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    OPEN(1,FILE=OUTDIR//'temicm.inp',FORM='UNFORMATTED' &
           ,STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
  ENDIF
  IF( ISTICM == 1 .AND. ISDICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'SALICM.ASC',STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    OPEN(1,FILE=OUTDIR//'TEMICM.ASC',STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
  ENDIF
  OPEN(1,FILE=OUTDIR//'efdcflw.inp',FORM='UNFORMATTED' &
           ,STATUS='UNKNOWN')
  CLOSE(1,STATUS='DELETE')
  OPEN(1,FILE=OUTDIR//'efdcrme.inp',FORM='UNFORMATTED' &
           ,STATUS='UNKNOWN')
  CLOSE(1,STATUS='DELETE')
  IF( ISDICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'EFDCFLW.ASC',STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
    OPEN(1,FILE=OUTDIR//'EFDCRME.ASC',STATUS='UNKNOWN')
    CLOSE(1,STATUS='DELETE')
  ENDIF
  !
  ! **  WRITE SUMMARY DATA TO LOG FILE
  !
  OPEN(1,FILE=OUTDIR//'EFDCICM.LOG',STATUS='UNKNOWN')
  CLOSE(1,STATUS='DELETE')
  OPEN(1,FILE=OUTDIR//'EFDCICM.LOG',STATUS='UNKNOWN')
  ICICM1=IDMPCL
  ICICM2=JDMPCL
  ICICM3=ISDICM
  ICICM4=IAUXICM
  NCICM1=NINTFL
  WRITE(1,2001) TIME
  WRITE(1,102)IC,JC,KC
  WRITE(1,103)NINTFL
  WRITE(1,104)IDMPCL,JDMPCL
  WRITE(1,2002)
  WRITE(1,2003)
  WRITE(1,2004)
  WRITE(1,2005)
  WRITE(1,2006)
  WRITE(1,2007)
  CLOSE(1)
  JSWASP=0
  RETURN
   1000 CONTINUE
  !
  ! **  SET VECTOR POTENTIAL TRANSPORT SWITCH TO INCLUDE VECTOR POTENTIAL
  !
  SVPT=0.
  IF( NTSMMT >= NTSPTC) SVPT=1.
  IF( ISDYNSTP == 0 )THEN
    TIME=(DT*FLOAT(N)+TCON*TBEGIN)/86400.
  ELSE
    TIME=TIMESEC/86400.
  ENDIF
  NMID=N-(NTSMMT/2)
  TIMMID=(DT*FLOAT(NMID)+TCON*TBEGIN)/86400.
  !
  ! **  WRITE TIME AT END OF AVERAGING PERIOD TO EFDCICM.LOG
  !
  OPEN(1,FILE=OUTDIR//'EFDCICM.LOG',STATUS='UNKNOWN' &
       ,POSITION='APPEND')
  WRITE(1,106)TIME
  WRITE(1,2008)
  WRITE(1,2009)
  WRITE(1,2010)
  WRITE(1,2011)
  CLOSE(1)
  !
  ! **  WRITE INFLOWS AT END OF AVERAGING PERIOD TO INFLOW.DAT
  !
  IF( IAUXICM >= 1 )THEN
    OPEN(1,FILE=OUTDIR//'INFLOW.DAT',STATUS='UNKNOWN' &
          ,POSITION='APPEND')
    DO NS=1,NQSIJ
      QINRCA(NS)=0.
    ENDDO
    DO NS=1,NQSIJ
      NQSTMP=NQSERQ(NS)
      IF( NQSTMP > 0 )THEN
        DO K=1,KC
          QINRCA(NS)=QINRCA(NS)+QSRTLPP(K,NQSTMP)+MAX(QSS(K,NS),0.)
        ENDDO
      ELSE
        DO K=1,KC
          QINRCA(NS)=QINRCA(NS)+MAX(QSS(K,NS),0.)
        ENDDO
      ENDIF
    ENDDO
    WRITE(1,120)TIME,(QINRCA(NS),NS=1,NQSIJ)
    CLOSE(1)
  ENDIF
  !
  ! **  WRITE INTERNAL FLOWS TO INTFLW.INP
  !
  IF( IAUXICM >= 1 )THEN
    OPEN(1,FILE=OUTDIR//'intflw.inp',FORM='UNFORMATTED' &
          ,STATUS='UNKNOWN', POSITION='APPEND')
    IF( ISDICM == 1 )THEN
      OPEN(2,FILE=OUTDIR//'INTFLW.ASC',STATUS='UNKNOWN' &
            ,POSITION='APPEND')
      WRITE(2,106)TIME
    ENDIF
    DO K=1,KC
      DO NS=1,NCRCA1
        QINTFL(NS,K)=0.
      ENDDO
    ENDDO
    NINTFL=0
    IF( NQSIJ > 0 )THEN
      DO NS=1,NQSIJ
        NINTFL=NINTFL+1
        NQSTMP=NQSERQ(NS)
        IF( NQSTMP > 0 )THEN
          DO K=1,KC
            QINTFL(NINTFL,K)=QINTFL(NINTFL,K)+QSRTLPN(K,NQSTMP) &
                +MIN(QSS(K,NS),0.)
          ENDDO
        ELSE
          DO K=1,KC
            QINTFL(NINTFL,K)=QINTFL(NINTFL,K)+MIN(QSS(K,NS),0.)
          ENDDO
        ENDIF
      ENDDO
    ENDIF
    IF( NQCTL > 0 )THEN
      DO NCTL=1,NQCTL
        NINTFL=NINTFL+1
        DO K=1,KC
          QINTFL(NINTFL,K)=QINTFL(NINTFL,K)+QCTLTLP(K,NCTL)
        ENDDO
      ENDDO
    ENDIF
    IF( NQWR > 0 )THEN
      DO NWR=1,NQWR
        NQSTMP=NQWRSERQ(NWR)
        NINTFL=NINTFL+1
        DO K=1,KC
          IF( K == KQWRU(NWR) )THEN
            QINTFL(NINTFL,K)=QINTFL(NINTFL,K)+QWRSERTLP(NQSTMP) &
                +QWR(NWR)
          ENDIF
        ENDDO
      ENDDO
    ENDIF
    IF( MDCHH > 0 )THEN
      DO NMD=1,MDCHH
        IF( IMDCHU(NMD) > 1 )THEN
          NINTFL=NINTFL+1
          DO K=1,KC
            QINTFL(NINTFL,K)=QINTFL(NINTFL,K)+QCHNULP(NMD)*DZC(L,K)
          ENDDO
        ENDIF
      ENDDO
      DO NMD=1,MDCHH
        IF( IMDCHV(NMD) > 1 )THEN
          NINTFL=NINTFL+1
          DO K=1,KC
            QINTFL(NINTFL,K)=QINTFL(NINTFL,K)+QCHNULP(NMD)*DZC(L,K)
          ENDDO
        ENDIF
      ENDDO
    ENDIF
    WRITE(1) QINTFL
    IF( ISDICM == 1 )THEN
      WRITE(2,213)
      DO NS=1,NINTFL
        WRITE(2,211)NS,(QINTFL(NS,K),K=1,KC)
      ENDDO
    ENDIF
    CLOSE(1)
    IF( ISDICM == 1 ) CLOSE(2)
  ENDIF
  !
  ! **  WRITE HYDRODYNAMIC DATA TO EFDCHDY.INP
  !
  OPEN(1,FILE=OUTDIR//'efdchyd.inp',FORM='UNFORMATTED' &
      ,STATUS='UNKNOWN',  POSITION='APPEND')
  WRITE(1) TIME
  OPEN(3,FILE=OUTDIR//'efdcflw.inp',FORM='UNFORMATTED' &
      ,STATUS='UNKNOWN',  POSITION='APPEND')
  WRITE(3) TIME
  OPEN(13,FILE=OUTDIR//'efdcrme.inp',FORM='UNFORMATTED' &
      ,STATUS='UNKNOWN',  POSITION='APPEND')
  WRITE(13) TIME
  IF( ISDICM == 1 )THEN
    OPEN(2,FILE=OUTDIR//'EFDCHYD.ASC',STATUS='UNKNOWN' &
          ,POSITION='APPEND')
    WRITE(2,106) TIME
    OPEN(4,FILE=OUTDIR//'EFDCFLW.ASC',STATUS='UNKNOWN' &
          ,POSITION='APPEND')
    WRITE(4,106) TIME
    OPEN(14,FILE=OUTDIR//'EFDCRME.ASC',STATUS='UNKNOWN' &
           ,POSITION='APPEND')
    WRITE(14,106) TIME
  ENDIF
  TAVGTMP=FLOAT(NTSMMT)*DT
  !
  ! **  WRITE CELL HORIZONTAL DEPTH AVERAGE VOLUMES
  !
  DO L=1,LC
    TVAR3E(L)=0.
    TVAR3W(L)=0.
    TVAR3S(L)=0.
    TVAR3N(L)=0.
    TVAR3C(L)=0.
  ENDDO
  DO K=1,KC
    DO L=2,LA
      TVAR3W(L)=TVAR3W(L)+DYU(L)*UHLPF(L,K)
      TVAR2W(L,K)=DYU(L)*UHLPF(L,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO L=2,LA
      LE=LEC(L)
      TVAR3E(L)=TVAR3E(L)+DYU(LE)*UHLPF(LE,K)
      TVAR2E(L,K)=DYU(LE)*UHLPF(LE,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO L=2,LA
      TVAR3S(L)=TVAR3S(L)+DXV(L)*VHLPF(L,K)
      TVAR2S(L,K)=DXV(L)*VHLPF(L,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO L=2,LA
      LN=LNC(L)
      TVAR3N(L)=TVAR3N(L)+DXV(LN)*VHLPF(LN,K)
      TVAR2N(L,K)=DXV(LN)*VHLPF(LN,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO L=2,LA
      TVAR3N(L)=TVAR3N(L)*DZC(L,K)
      TVAR3S(L)=TVAR3S(L)*DZC(L,K)
      TVAR3E(L)=TVAR3E(L)*DZC(L,K)
      TVAR3W(L)=TVAR3W(L)*DZC(L,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO L=2,LA
      TVAR2N(L,K)=TVAR2N(L,K)*DZC(L,K)
      TVAR2S(L,K)=TVAR2S(L,K)*DZC(L,K)
      TVAR2E(L,K)=TVAR2E(L,K)*DZC(L,K)
      TVAR2W(L,K)=TVAR2W(L,K)*DZC(L,K)
    ENDDO
  ENDDO
  TMPVAL=DT*FLOAT(NTSMMT)
  DO L=2,LA
    WLPF(L,0)=0.
    WLPF(L,KC)=0.
    TVAR2C(L,0)=0.
  ENDDO
  DO K=1,KS
    DO L=2,LA
      WLPF(L,K)=WLPF(L,K-1)+TVAR2W(L,K)-TVAR2E(L,K) &
          +TVAR2S(L,K)-TVAR2N(L,K) &
          +QSUMLPF(L,K)
    ENDDO
  ENDDO
  DO L=2,LA
    TVAR3C(L)=WLPF(L,KS)+TVAR2W(L,KC)-TVAR2E(L,KC) &
        +TVAR2S(L,KC)-TVAR2N(L,KC) &
        +QSUMLPF(L,KC)
  ENDDO
  DO K=1,KS
    DO L=2,LA
      TVAR2C(L,K)=TVAR2C(L,K-1)+DZC(L,K)*TVAR3C(L)
    ENDDO
  ENDDO
  DO K=1,KS
    DO L=2,LA
      WLPF(L,K)=WLPF(L,K)-TVAR2C(L,K)
    ENDDO
  ENDDO
  DO L=2,LA
    TVAR3C(L)=TMPVAL*TVAR3C(L)
  ENDDO
  DO L=2,LA
    TVAR3C(L)=DXYP(L)*HTMP(L)+TVAR3C(L)
  ENDDO
  DO L=2,LA
    HTMP(L)=TVAR3C(L)*DXYIP(L)
  ENDDO
  WRITE(1) (TVAR3C(L),L=2,LA)
  DO L=2,LA
    QSUMLPF(L,KC)=QSUMLPF(L,KC)-RAINLPF(L)+EVPSLPF(L)
  ENDDO
  DO K=KC,1,-1
    WRITE(3) (QSUMLPF(L,K),L=2,LA)
  ENDDO
  IF( ISDICM == 1 )THEN
    WRITE(2,2008)
    WRITE(4,401)
    DO L=2,LA
      LM=LWC(L)
      TMPVAL=DXYP(L)*HWQ(L)
      WRITE(2,2294)LM,L,HP(L),HWQ(L),HTMP(L)
      WRITE(4,402)LM,L,(QSUMLPF(L,K),K=1,KC)
  !
  !             RVAL2=-DYU(LP)*UHLPF(LP,K)*DZC(L,K)
  !             RVAL4=-DXV(LN)*VHLPF(LN,K)*DZC(L,K)
  !
    ENDDO
  ENDIF
  DO L=2,LA
    TVAR3C(L)=RAINLPF(L)-EVPSLPF(L)
  ENDDO
  WRITE(13) (TVAR3C(L),L=2,LA)
  DO L=2,LA
    LM=LWC(L)
    WRITE(14,402)LM,L,TVAR3C(L)
  ENDDO
    222 FORMAT(' ERROR ',2I5,6F12.2)
    401 FORMAT(/,'  LICM     L    QSUMLPF(L,K) K=1,KC',/)
    402 FORMAT(2I6,12E13.5)
   2294 FORMAT(2I6,4F12.5)
  !
  ! **  WRITE HORIZONTAL FLOWS
  !
  DO M=1,NFICM
    L=LFEFDC(M)
    K=KFEFDC(M)
    IF( IDRICM(M) == 1 )THEN
      TMPICMF(M)=DYU(L)*(UHLPF(L,K)+SVPT*UVPT(L,K))*DZC(L,K)
    ENDIF
    IF( IDRICM(M) == 2 )THEN
      TMPICMF(M)=DXV(L)*(VHLPF(L,K)+SVPT*VVPT(L,K))*DZC(L,K)
    ENDIF
  ENDDO
  WRITE(1) (TMPICMF(M),M=1,NFICM)
  IF( ISDICM == 1 )THEN
    WRITE(2,2009)
    DO M=1,NFICM
      L=LFEFDC(M)
      K=KFEFDC(M)
      WRITE(2,293)M,TMPICMF(M),IDRICM(M),L,K
    ENDDO
  ENDIF
  !
  ! **  WRITE VERTICAL DIFFUSIVITY
  !
  MM=0
  DO K=KS,1,-1
    DO L=2,LA
      MM=MM+1
      TMPICMF(MM)=ABLPF(L,K)
    ENDDO
  ENDDO
  WRITE(1) (TMPICMF(M),M=1,MM)
  IF( ISDICM == 1 )THEN
    WRITE(2,2010)
    MM=0
    DO K=KS,1,-1
      DO L=2,LA
        MM=MM+1
        WRITE(2,293)MM,ABLPF(L,K),L,K
      ENDDO
    ENDDO
  ENDIF
  !
  ! **  DETERMINATION OF VERTICAL VELOCITY AND VOLUME FLUX
  ! **  LOAD NET DEPTH INTEGRATED INFLOWS INTO TVAR3E AND
  ! **  OUT FLOWS INTO TVAR3N
  !       WLPF(L,0)=0.
  !        TVAR1N(L,K)=0.
  !        TVAR1S(L,K)=0.
  !        TVAR1E(L,K)=0.
  !        TVAR1W(L,K)=0.
  ! **  CALCULATE QZ (STORED IN WLPF(L,K)
  ! **  WRITE VERTICAL VOLUME FLUX
  !
  MM=0
  DO K=KS,1,-1
    DO L=2,LA
      MM=MM+1
      TMPICMF(MM)=WLPF(L,K)
    ENDDO
  ENDDO
  WRITE(1) (TMPICMF(M),M=1,MM)
  IF( ISDICM == 1 )THEN
    WRITE(2,2011)
    MM=0
    DO K=KS,1,-1
      DO L=2,LA
        MM=MM+1
        WRITE(2,293)MM,WLPF(L,K),L,K
      ENDDO
    ENDDO
    MM=0
    DO L=2,LA
      WRITE(2,293)MM,WLPF(L,KC),L,KC
    ENDDO
  ENDIF
  CLOSE(1)
  CLOSE(3)
  CLOSE(13)
  IF( ISDICM == 1 ) CLOSE(2)
  IF( ISDICM == 1 ) CLOSE(4)
  IF( ISDICM == 1 ) CLOSE(14)
  !
  ! **  WRITE SALINITY
  !
  IF( ISTICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'salicm.inp',FORM='UNFORMATTED' &
          ,POSITION='APPEND')
    MM=0
    DO K=KS,1,-1
      DO L=2,LA
        MM=MM+1
        TMPICMF(MM)=SALLPF(L,K)
      ENDDO
    ENDDO
    WRITE(1) (TMPICMF(M),M=1,MM)
    CLOSE(1)
  ENDIF
  IF( ISTICM == 1 .AND. ISDICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'SALICM.ASC',POSITION='APPEND')
    WRITE(1,106) TIME
    WRITE(1,2012)
    MM=0
    DO K=KS,1,-1
      DO L=2,LA
        MM=MM+1
        WRITE(2,293)MM,SALLPF(L,K),L,K
      ENDDO
    ENDDO
    CLOSE(1)
  ENDIF
  !
  ! **  WRITE TEMPERATURE
  !
  IF( ISTICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'temicm.inp',FORM='UNFORMATTED' &
          ,POSITION='APPEND')
    MM=0
    DO K=KS,1,-1
      DO L=2,LA
        MM=MM+1
        TMPICMF(MM)=TEMLPF(L,K)
      ENDDO
    ENDDO
    WRITE(1) (TMPICMF(M),M=1,MM)
    CLOSE(1)
  ENDIF
  IF( ISTICM == 1 .AND. ISDICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'TEMICM.ASC',POSITION='APPEND')
    WRITE(1,106) TIME
    WRITE(1,2013)
    MM=0
    DO K=KS,1,-1
      DO L=2,LA
        MM=MM+1
        WRITE(2,293)MM,TEMLPF(L,K),L,K
      ENDDO
    ENDDO
    CLOSE(1)
  ENDIF
  !
  ! **  WRITE HYDROLOGY
  !
  IF( IAUXICM == 1 )THEN
    OPEN(1,FILE=OUTDIR//'hydrlgy.inp',FORM='UNFORMATTED' &
          ,POSITION='APPEND')
    WRITE(1)TIME
    MM=0
    DO L=2,LA
      MM=MM+1
      TVAR3C(MM)=RAINLPF(L)
    ENDDO
    WRITE(1) (TVAR3C(M),M=1,MM)
    MM=0
    DO L=2,LA
      MM=MM+1
      TVAR3C(MM)=EVPSLPF(L)
    ENDDO
    WRITE(1) (TVAR3C(M),M=1,MM)
    MM=0
    DO L=2,LA
      MM=MM+1
      TVAR3C(MM)=RINFLPF(L)
    ENDDO
    WRITE(1) (TVAR3C(M),M=1,MM)
    CLOSE(1)
    IF( ISDICM == 1 )THEN
      OPEN(2,FILE=OUTDIR//'HYDRLGY.ASC',POSITION='APPEND')
      WRITE(2,106)TIME
      WRITE(2,212)
      DO L=2,LA
        WRITE(2,200)L,IL(L),JL(L),RAINLPF(L),EVPSLPF(L),EVPGLPF(L), &
            RINFLPF(L),GWLPF(L)
      ENDDO
      CLOSE(2)
    ENDIF
  ENDIF
    100 FORMAT(120X)
    101 FORMAT(4I10)
    102 FORMAT(/,' NROW,NCOL,NLAYR = ',3I10/)
    103 FORMAT(/,' NO INTERNAL FLOWS, NINTFL (LINES) IN FLWMAP.INP = ', &
      I10/)
    104 FORMAT(/,' ROW, COLUMN INDICES OF DUMP CELL = ',2I10/)
    105 FORMAT(/,' SIMULATION STARTING TIME IN DAYS = ',F12.6/)
    106 FORMAT(/,' TIME IN DAYS AT END OF AVERAGING PERIOD = ',F12.6/)
    110 FORMAT(' LOCATION OF INFLOWS ',/)
    111 FORMAT(' INFLOW #   ROW INDEX   COLUMN INDEX ',/)
    112 FORMAT(2X,I5,7X,I5,7X,I5)
    120 FORMAT(F12.6,13F12.4)
    200 FORMAT(3I5,6E14.6)
    201 FORMAT(' L,I(ROW),J(COL),QX(I,J,K),K=1,KC ',/)
    202 FORMAT(' L,I(ROW),J(COL),QY(I,J,K),K=1,KC ',/)
    203 FORMAT(' L,I(ROW),J(COL),QZ(I,J,K),K=1,KS ',/)
    204 FORMAT(' L,I(ROW),J(COL),AX(I,J,K),K=1,KC ',/)
    205 FORMAT(' L,I(ROW),J(COL),AY(I,J,K),K=1,KC ',/)
    206 FORMAT(' L,I(ROW),J(COL),AZ(I,J,K),K=1,KS ',/)
    207 FORMAT(' L,I(ROW),J(COL),SELS(I,J),SELE(I,J),DSEL(I,J) ',/)
    208 FORMAT(' L,I(ROW),J(COL),SAL(I,J,K),K=1,KC ',/)
    209 FORMAT(' L,I(ROW),J(COL),TEM(I,J,K),K=1,KC ',/)
    210 FORMAT(//)
    211 FORMAT(I5,2X,6E15.6)
    212 FORMAT(' L,I(ROW),J(ROW),RAINLPF(I,J),EVPSLPF(I,J),EVPGLPF(I,J), &
      RINFLPF(I,J),GWLPF(I,J) ',/)
    213 FORMAT(' NQINTFL,QINTFL ',/)
    215 FORMAT(' L,I(ROW),J(COL),SURFELV START AVG INTERVAL',/)
    216 FORMAT(' L,I(ROW),J(COL),DEL SURFELV OVER INTERVAL',/)
    291 FORMAT(I8,F8.4)
    292 FORMAT(I8,E13.5)
    293 FORMAT(I8,E13.5,3I8)
    294 FORMAT(I8,E13.5,E13.5,3I8)
   2001 FORMAT(/,' TIME AT ICM INTERFACE INITIALIZATION = ',F12.4,/)
   2002 FORMAT(/,' SIGMA LAYER FRACTIONAL THICKNESS: KICM, DZ, KEFDC',/)
   2003 FORMAT(/,' HORIZONTAL CELL SURFACE AREAS, TOP LAYER : LICM, AREA' &
      ,' LEFDC',/)
   2004 FORMAT(/,' DEPTH INTEGRATED HORIZONTAL FLOW FACE AREAS: NF, AREA' &
      ,' IDIR, LEFDC',/)
   2005 FORMAT(/,' X DIRECTION CELL LENTHS, TOP LAYER: LICM, DX, LEFDC',/)
   2006 FORMAT(/,' Y DIRECTION CELL LENTHS, TOP LAYER: LICM, DY, LEFDC',/)
   2007 FORMAT(/,' INITIAL DEPTH AVERGE CELL VOLUMES: LICM, VOL, LEFDC',/)
   2008 FORMAT(/,' DEPTH AVERGE CELL VOLUMES: LICM, VOL, LEFDC',/)
   2009 FORMAT(/,' HORIZONTAL FLOWS: NF, FLOW, IDIR, LEFDC, KEFDC'/)
   2010 FORMAT(/,' VERTICAL DIFFUSIVITY: ND, DIFF, LEFDC, KEFDC',/)
   2011 FORMAT(/,' VERTICAL SIGMA FLOW: ND, FLOWZ, LEFDC, KEFDC',/)
   2012 FORMAT(/,' HYDRO SALINITY: NICM, SAL, LEFDC, KEFDC',/)
   2013 FORMAT(/,' HYDRO SALINITY: NICM, TEM, LEFDC, KEFDC',/)
  RETURN
END

