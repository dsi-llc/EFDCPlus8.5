SUBROUTINE CBALEV1

  ! CHANGE RECORD

  ! **  SUBROUTINES CBALEV CALCULATE GLOBAL VOLUME, MASS, MOMENTUM,
  ! **  AND ENERGY BALANCES
  !
  USE GLOBAL
  IMPLICIT NONE
  INTEGER :: L,LN,K

  IF( NBALE > 1) RETURN
  !
  ! **  INITIALIZE VOLUME, SALT MASS, DYE MASS, MOMENTUM, KINETIC ENERGY
  ! **  AND POTENTIAL ENERGY, AND ASSOCIATED FLUXES
  !
  VOLBEGE=0.
  SALBEGE=0.
  DYEBEGE=0.
  UMOBEGE=0.
  VMOBEGE=0.
  UUEBEGE=0.
  VVEBEGE=0.
  PPEBEGE=0.
  BBEBEGE=0.
  VOLOUTE=0.
  SALOUTE=0.
  DYEOUTE=0.
  UMOOUTE=0.
  VMOOUTE=0.
  UUEOUTE=0.
  VVEOUTE=0.
  PPEOUTE=0.
  BBEOUTE=0.
  DO L=2,LA
    LN=LNC(L)
    VOLBEGE=VOLBEGE+SPB(L)*DXYP(L)*H1P(L)
    UMOBEGE=UMOBEGE+SPB(L)*0.5*DXYP(L)*H1P(L)*(DYIU(L)*UHDY1E(L)/H1U(L)+DYIU(LEC(L))*UHDY1E(LEC(L))/H1U(LEC(L)))
    VMOBEGE=VMOBEGE+SPB(L)*0.5*DXYP(L)*H1P(L)*(DXIV(L)*VHDX1E(L)/H1V(L)+DXIV(LN) *VHDX1E(LN) /H1V(LN))
    PPEBEGE=PPEBEGE+SPB(L)*0.5*DXYP(L)*(GI*P1(L)*P1(L)-G*BELV(L)*BELV(L))
  ENDDO
  AMOBEGE=SQRT(UMOBEGE*UMOBEGE+VMOBEGE*VMOBEGE)
  DO K=1,KC
    DO L=2,LA
      LN=LNC(L)
      SALBEGE=SALBEGE+SCB(L)*DXYP(L)*H1P(L)*SAL1(L,K)*DZC(L,K)
      DYEBEGE=DYEBEGE+SCB(L)*DXYP(L)*H1P(L)*DYE1(L,K)*DZC(L,K)
      UUEBEGE=UUEBEGE+SPB(L)*0.125*DXYP(L)*H1P(L)*DZC(L,K)*( (U1(L,K)+U1(LEC(L),K))*(U1(L,K)+U1(LEC(L),K)) )
      VVEBEGE=VVEBEGE+SPB(L)*0.125*DXYP(L)*H1P(L)*DZC(L,K)*( (V1(L,K)+V1(LN,K))*(V1(L,K)+V1(LN,K)) )
      BBEBEGE=BBEBEGE+SPB(L)*GP*DXYP(L)*H1P(L)*DZC(L,K)*( BELV(L)+0.5*H1P(L)*(Z(L,K)+Z(L,K-1)) )*B1(L,K)
    ENDDO
  ENDDO
  RETURN
  END

  SUBROUTINE CBALEV2
  !
  ! CHANGE RECORD
  ! **  SUBROUTINES CBALEV CALCULATE GLOBAL VOLUME, MASS, MOMENTUM,
  ! **  AND ENERGY BALANCES
  !
  USE GLOBAL
  IMPLICIT NONE
  INTEGER :: L,LN,K,LL,LS

  ! **  ACCUMULATE FLUXES ACROSS OPEN BOUNDARIES
  !
  DO K=1,KC
    DO LL=1,NCBS
      L=LCBS(LL)
      LN=LNC(L)
      VOLOUTE=VOLOUTE-VHDX2(LN,K)
      SALOUTE=SALOUTE-MIN(VHDX2(LN,K),0.)*SAL1(LN,K)-MAX(VHDX2(LN,K),0.)*SAL1(L,K)
      DYEOUTE=DYEOUTE-MIN(VHDX2(LN,K),0.)*DYE1(LN,K)-MAX(VHDX2(LN,K),0.)*DYE1(L,K)
      PPEOUTE=PPEOUTE-VHDX2(LN,K)*G*( 0.5*(BELV(L)+BELV(LN))+0.125*(HP(L)+H2P(L)+HP(LN)+H2P(LN))*(Z(L,K)+Z(L,K-1)) )
      BBEOUTE=BBEOUTE-MIN(VHDX2(LN,K),0.)*GP*( BELV(LN)+0.5*HP(LN)*(Z(L,K)+Z(L,K-1)) )*B1(LN,K)-MAX(VHDX2(LN,K),0.)*GP*( BELV(L)+0.5*HP(L)*(Z(L,K)+Z(L,K-1)) )*B1(L,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO LL=1,NCBW
      L=LCBW(LL)
      VOLOUTE=VOLOUTE-UHDY2(LEC(L),K)
      SALOUTE=SALOUTE-MIN(UHDY2(LEC(L),K),0.)*SAL1(LEC(L),K)-MAX(UHDY2(LEC(L),K),0.)*SAL1(L,K)
      DYEOUTE=DYEOUTE-MIN(UHDY2(LEC(L),K),0.)*DYE1(LEC(L),K)-MAX(UHDY2(LEC(L),K),0.)*DYE1(L,K)
      PPEOUTE=PPEOUTE-UHDY2(LEC(L),K)*G*(0.5*(BELV(L)+BELV(LEC(L)))+0.125*(HP(L)+H2P(L)+HP(LEC(L))+H2P(LEC(L)))*(Z(L,K)+Z(L,K-1)) )
      BBEOUTE=BBEOUTE-MIN(UHDY2(LEC(L),K),0.)*GP*( BELV(LEC(L))+0.5*HP(LEC(L))*(Z(L,K)+Z(L,K-1)) )*B1(LEC(L),K)-MAX(UHDY2(LEC(L),K),0.)*GP*( BELV(L)+0.5*HP(L)*(Z(L,K)+Z(L,K-1)) )*B1(L,K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO LL=1,NCBE
      L=LCBE(LL)
      VOLOUTE=VOLOUTE+UHDY2(L,K)
      SALOUTE=SALOUTE+MIN(UHDY2(L,K),0.)*SAL1(L,K)+MAX(UHDY2(L,K),0.)*SAL1(LWC(L),K)
      DYEOUTE=DYEOUTE+MIN(UHDY2(L,K),0.)*DYE1(L,K)+MAX(UHDY2(L,K),0.)*DYE1(LWC(L),K)
      PPEOUTE=PPEOUTE+UHDY2(L,K)*G*( 0.5*(BELV(L)+BELV(LWC(L)))+0.125*(HP(L)+H2P(L)+HP(LWC(L))+H2P(LWC(L)))*(Z(L,K)+Z(L,K-1)) )
      BBEOUTE=BBEOUTE+MIN(UHDY2(L,K),0.)*GP*(BELV(L)+0.5*HP(L)*(Z(L,K)+Z(L,K-1)) )*B1(L,K)+MAX(UHDY2(L,K),0.)*GP*(BELV(LWC(L))+0.5*HP(LWC(L))*(Z(L,K)+Z(L,K-1)) )*B1(LWC(L),K)
    ENDDO
  ENDDO
  DO K=1,KC
    DO LL=1,NCBN
      L=LCBN(LL)
      LS=LSC(L)
      VOLOUTE=VOLOUTE+VHDX2(L,K)
      SALOUTE=SALOUTE+MIN(VHDX2(L,K),0.)*SAL1(L,K)+MAX(VHDX2(L,K),0.)*SAL1(LS,K)
      DYEOUTE=DYEOUTE+MIN(VHDX2(L,K),0.)*DYE1(L,K)+MAX(VHDX2(L,K),0.)*DYE1(LS,K)
      PPEOUTE=PPEOUTE+VHDX2(L,K)*G*( 0.5*(BELV(L)+BELV(LS))+0.125*(HP(L)+H2P(L)+HP(LS)+H2P(LS))*(Z(L,K)+Z(L,K-1)) )
      BBEOUTE=BBEOUTE+MIN(VHDX2(L,K),0.)*GP*( BELV(L)+0.5*HP(L)*(Z(L,K)+Z(L,K-1)) )*B1(L,K)+MAX(VHDX2(L,K),0.)*GP*( BELV(LS)+0.5*HP(LS)*(Z(L,K)+Z(L,K-1)) )*B1(LS,K)
    ENDDO
  ENDDO
  RETURN
END

SUBROUTINE CBALEV3
  !
  ! CHANGE RECORD
  ! **  SUBROUTINES CBALEV CALCULATE GLOBAL VOLUME, MASS, MOMENTUM,
  ! **  AND ENERGY BALANCES
  !
  USE GLOBAL
  IMPLICIT NONE
  INTEGER :: L,K,LL,NS,NQSTMP,NCSTMP,NCTL,IU,JU,LU,ID,JD,LD,NWR,KU,KD
  REAL    :: QWRABS,RQWD
  !
  ! **  ACCUMULATE INTERNAL SOURCES AND SINKS
  !
  DO L=2,LA
    VOLOUTE=VOLOUTE-QSUME(L)
  ENDDO
  DO K=1,KC
    DO LL=1,NQSIJ
      L=LQS(LL)
      PPEOUTE=PPEOUTE-QSS(K,LL)*G*( 0.5*(BELV(L)+BELV(LWC(L)))+0.125*(HP(L)+H2P(L)+HP(LWC(L))+H2P(LWC(L)))*(Z(L,K)+Z(L,K-1)) )
    ENDDO
  ENDDO
  IF( ISTRAN(1) >= 1 )THEN
    DO K=1,KC
      DO L=2,LC
        CONT(L,K)=SAL(L,K)
      ENDDO
    ENDDO
    DO NS=1,NQSIJ
      L=LQS(NS)
      NQSTMP=NQSERQ(NS)
      NCSTMP=NCSERQ(NS,1)
      DO K=1,KC
        SALOUTE=SALOUTE-MAX(QSS(K,NS),0.)*CQS(K,NS,1)-MIN(QSS(K,NS),0.)*SAL(L,K)-MAX(QSERCELL(K,NS),0.)*CSERT(K,NCSTMP,1)-MIN(QSERCELL(K,NS),0.)*SAL(L,K)
      ENDDO
    ENDDO
    DO NCTL=1,NQCTL
      RQWD=1.
      IU=IQCTLU(NCTL)
      JU=JQCTLU(NCTL)
      LU=LIJ(IU,JU)
      ID=IQCTLD(NCTL)
      JD=JQCTLD(NCTL)
      IF( ID == 0 .AND. JD == 0 )THEN
        LD=LC
        RQWD=0.
      ELSE
        LD=LIJ(ID,JD)
      ENDIF
      DO K=1,KC
        SALOUTE=SALOUTE+QCTLT(K,NCTL,1)*CONT(LU,K)-RQWD*QCTLT(K,NCTL,1)*CONT(LU,K)
      ENDDO
    ENDDO
    DO NWR=1,NQWR
      ! *** Handle +/- Flows for Withdrawal/Return Structures
      NQSTMP=NQWRSERQ(NWR)
      IF( QWRSERT(NQSTMP) >= 0. )THEN
        ! *** Original Withdrawal/Return
        IU=IQWRU(NWR)
        JU=JQWRU(NWR)
        KU=KQWRU(NWR)
        ID=IQWRD(NWR)
        JD=JQWRD(NWR)
        KD=KQWRD(NWR)
      ELSE
        ! *** Reverse Flow Withdrawal/Return
        ID=IQWRU(NWR)
        JD=JQWRU(NWR)
        KD=KQWRU(NWR)
        IU=IQWRD(NWR)
        JU=JQWRD(NWR)
        KU=KQWRD(NWR)
      ENDIF
      QWRABS = ABS(QWRSERT(NQSTMP))
      LU=LIJ(IU,JU)
      LD=LIJ(ID,JD)
      NCSTMP=NQWRSERQ(NWR)

      SALOUTE=SALOUTE+( (QWR(NWR)+QWRABS)*CONT(LU,KU) )
      IF( LD /= 1 .OR. LD /= LC )THEN
        SALOUTE=SALOUTE-( QWR(NWR)*(CONT(LU,KU)+CQWR(NWR,1))+QSERCELL(K,NS)*(CONT(LU,KU)+CQWRSERT(NCSTMP,1)) )
      ENDIF
    ENDDO
  ENDIF
  IF( ISTRAN(3) >= 1 )THEN
    DO K=1,KC
      DO L=2,LC
        CONT(L,K)=DYE(L,K)
      ENDDO
    ENDDO
    DO NS=1,NQSIJ
      L=LQS(NS)
      NQSTMP=NQSERQ(NS)
      NCSTMP=NCSERQ(NS,1)
      DO K=1,KC
        DYEOUTE=DYEOUTE-MAX(QSS(K,NS),0.)*CQS(K,NS,3)-MIN(QSS(K,NS),0.)*DYE(L,K)-MAX(QSERCELL(K,NS),0.)*CSERT(K,NCSTMP,3)-MIN(QSERCELL(K,NS),0.)*DYE(L,K)
      ENDDO
    ENDDO
    DO NCTL=1,NQCTL
      RQWD=1.
      IU=IQCTLU(NCTL)
      JU=JQCTLU(NCTL)
      LU=LIJ(IU,JU)
      ID=IQCTLD(NCTL)
      JD=JQCTLD(NCTL)
      IF( ID == 0 .AND. JD == 0 )THEN
        LD=LC
        RQWD=0.
      ELSE
        LD=LIJ(ID,JD)
      ENDIF
      DO K=1,KC
        DYEOUTE=DYEOUTE+QCTLT(K,NCTL,1)*CONT(LU,K)-RQWD*QCTLT(K,NCTL,1)*CONT(LU,K)
      ENDDO
    ENDDO
    DO NWR=1,NQWR
      ! *** Handle +/- Flows for Withdrawal/Return Structures
      NQSTMP=NQWRSERQ(NWR)
      IF( QWRSERT(NQSTMP) >= 0. )THEN
        ! *** Original Withdrawal/Return
        IU=IQWRU(NWR)
        JU=JQWRU(NWR)
        KU=KQWRU(NWR)
        ID=IQWRD(NWR)
        JD=JQWRD(NWR)
        KD=KQWRD(NWR)
      ELSE
        ! *** Reverse Flow Withdrawal/Return
        ID=IQWRU(NWR)
        JD=JQWRU(NWR)
        KD=KQWRU(NWR)
        IU=IQWRD(NWR)
        JU=JQWRD(NWR)
        KU=KQWRD(NWR)
      ENDIF
      QWRABS = ABS(QWRSERT(NQSTMP))
      LU=LIJ(IU,JU)
      LD=LIJ(ID,JD)
      NCSTMP=NQWRSERQ(NWR)

      DYEOUTE=DYEOUTE+( (QWR(NWR)+QWRABS)*CONT(LU,KU) )
      IF( LD /= 1 .OR. LD /= LC )THEN
        DYEOUTE=DYEOUTE-( QWR(NWR)*(CONT(LU,KU)+CQWR(NWR,3))+QWRABS*(CONT(LU,KU)+CQWRSERT(NCSTMP,3)) )
      ENDIF
    ENDDO
  ENDIF
  RETURN
END

SUBROUTINE CBALEV4
  !
  ! CHANGE RECORD
  ! **  SUBROUTINES CBALEV CALCULATE GLOBAL VOLUME, MASS, MOMENTUM,
  ! **  AND ENERGY BALANCES
  !
  USE GLOBAL
  IMPLICIT NONE
  INTEGER :: L,LN,K
  REAL    :: DUTMP,DVTMP

  ! **  CALCULATE MOMENTUM AND ENERGY DISSIPATION
  !
  DO L=2,LA
    LN=LNC(L)
    UUEOUTE=UUEOUTE+0.5*SPB(L)*DXYP(L)*(U(L,KSZ(L))*TBX(L) + U(LEC(L),KSZ(L))*TBX(LEC(L)) - U(L,KC)*TSX(L) - U(LEC(L),KC)*TSX(LEC(L)))
    VVEOUTE=VVEOUTE+0.5*SPB(L)*DXYP(L)*(V(L,KSZ(L))*TBY(L) + V(LN,KSZ(L)) *TBX(LN)  - V(L,KC)*TSY(L) - V(LN,KC)*TSX(LN))
  ENDDO
  DO K=1,KS
    DO L=2,LA
      LN=LNC(L)
      DUTMP=0.5*( U(L,K+1)+U(LEC(L),K+1)-U(L,K)-U(LEC(L),K) )
      DVTMP=0.5*( V(L,K+1)+V(LN,K+1)-V(L,K)-V(LN,K) )
      UUEOUTE=UUEOUTE+SPB(L)*2.0*DXYP(L)*AV(L,K)*( DUTMP*DUTMP )/(DZC(L,K+1)+DZC(L,K))
      VVEOUTE=VVEOUTE+SPB(L)*2.0*DXYP(L)*AV(L,K)*( DVTMP*DVTMP )/(DZC(L,K+1)+DZC(L,K))
      BBEOUTE=BBEOUTE+SCB(L)*DXYP(L)*HP(L)*GP*AB(L,K)*(B(L,K+1)-B(L,K))
    ENDDO
  ENDDO
  RETURN
END

SUBROUTINE CBALEV5
  !
  ! CHANGE RECORD
  ! **  SUBROUTINES CBALEV CALCULATE GLOBAL VOLUME, MASS, MOMENTUM,
  ! **  AND ENERGY BALANCES
  !
  USE GLOBAL
  INTEGER :: L,LN,K,NTMPD2
  REAL    :: VOLENDE,SALENDE,DYEENDE,UMOENDE,VMOENDE,UUEENDE,VVEENDE
  REAL    :: PPEENDE,BBEENDE,AMOENDE,ENEBEGE,ENEENDE,ENEOUTE,VOLBMOE,SALBMOE,DYEBMOE
  REAL    :: UMOBMOE,VMOBMOE,ENEBMOE,VOLERR,SALERR,DYEERR,UMOERR,VMOERR,ENEERR,RVERDE
  REAL    :: RSERDE,RDERDE,RUERDE,REERDE,RVERDO,RSERDO,RDERDO,RUERDO,REERDO,RUMERDE
  REAL    :: RVMERDE,RUMERDO,RVMERDO,UUEBMOE,VVEBMOE,PPEBMOE,BBEBMOE

  ! **  CHECK FOR END OF BALANCE PERIOD
  NTMPD2=NTSMMT/2
  IF( NBALE == NTMPD2 )THEN

    ! **  CALCULATE ENDING VOLUME, SALT MASS, DYE MASS, MOMENTUM, KINETIC
    ! **  ENERGY AND POTENTIAL ENERGY, AND ASSOCIATED FLUXES
    VOLENDE=0.
    SALENDE=0.
    DYEENDE=0.
    UMOENDE=0.
    VMOENDE=0.
    UUEENDE=0.
    VVEENDE=0.
    PPEENDE=0.
    BBEENDE=0.
    DO L=2,LA
      LN=LNC(L)
      VOLENDE=VOLENDE+SPB(L)*DXYP(L)*HP(L)
      UMOENDE=UMOENDE+SPB(L)*0.5*DXYP(L)*HP(L)*(DYIU(L)*HUI(L)*UHDYE(L)+DYIU(LEC(L))*HUI(LEC(L))*UHDYE(LEC(L)))
      VMOENDE=VMOENDE+SPB(L)*0.5*DXYP(L)*HP(L)*(DXIV(L)*HVI(L)*VHDXE(L)+DXIV(LN)*HVI(LN)*VHDXE(LN))
      PPEENDE=PPEENDE+SPB(L)*0.5*DXYP(L)*(GI*P(L)*P(L)-G*BELV(L)*BELV(L))
    ENDDO
    AMOENDE=SQRT(UMOENDE*UMOENDE+VMOENDE*VMOENDE)
    DO K=1,KC
      DO L=2,LA
        LN=LNC(L)
        SALENDE=SALENDE+SCB(L)*DXYP(L)*HP(L)*SAL(L,K)*DZC(L,K)
        DYEENDE=DYEENDE+SCB(L)*DXYP(L)*HP(L)*DYE(L,K)*DZC(L,K)
        UUEENDE=UUEENDE+SPB(L)*0.125*DXYP(L)*HP(L)*DZC(L,K)*( (U(L,K)+U(LEC(L),K))*(U(L,K)+U(LEC(L),K)) )
        VVEENDE=VVEENDE+SPB(L)*0.125*DXYP(L)*HP(L)*DZC(L,K)*( (V(L,K)+V(LN,K))*(V(L,K)+V(LN,K)) )
        BBEENDE=BBEENDE+SPB(L)*GP*DXYP(L)*HP(L)*DZC(L,K)*( BELV(L)+0.5*HP(L)*(Z(L,K)+Z(L,K-1)) )*B(L,K)
      ENDDO
    ENDDO
    UUEOUTE=DT2*UUEOUTE
    VVEOUTE=DT2*VVEOUTE
    PPEOUTE=DT2*PPEOUTE
    BBEOUTE=DT2*BBEOUTE
    VOLOUTE=DT2*VOLOUTE
    SALOUTE=DT2*SALOUTE
    DYEOUTE=DT2*DYEOUTE
    UMOOUTE=DT2*UMOOUTE
    VMOOUTE=DT2*VMOOUTE
    ENEBEGE=UUEBEGE+VVEBEGE+PPEBEGE+BBEBEGE
    ENEENDE=UUEENDE+VVEENDE+PPEENDE+BBEENDE
    ENEOUTE=UUEOUTE+VVEOUTE+PPEOUTE+BBEOUTE
    VOLBMOE=VOLBEGE-VOLOUTE
    SALBMOE=SALBEGE-SALOUTE
    DYEBMOE=DYEBEGE-DYEOUTE
    UMOBMOE=UMOBEGE-DYEOUTE
    VMOBMOE=VMOBEGE-DYEOUTE
    ENEBMOE=ENEBEGE-ENEOUTE
    VOLERR=VOLENDE-VOLBMOE
    SALERR=SALENDE-SALBMOE
    DYEERR=DYEENDE-DYEBMOE
    UMOERR=UMOENDE-UMOBMOE
    VMOERR=VMOENDE-VMOBMOE
    ENEERR=ENEENDE-ENEBMOE
    RVERDE=-9999.
    RSERDE=-9999.
    RDERDE=-9999.
    RUERDE=-9999.
    RVERDE=-9999.
    REERDE=-9999.
    RVERDO=-9999.
    RSERDO=-9999.
    RDERDO=-9999.
    RUERDO=-9999.
    RVERDO=-9999.
    REERDO=-9999.
    IF( VOLENDE /= 0. ) RVERDE=VOLERR/VOLENDE
    IF( SALENDE /= 0. ) RSERDE=SALERR/SALENDE
    IF( DYEENDE /= 0. ) RDERDE=DYEERR/DYEENDE
    IF( UMOENDE /= 0. ) RUMERDE=UMOERR/UMOENDE
    IF( VMOENDE /= 0. ) RVMERDE=VMOERR/VMOENDE
    IF( ENEENDE /= 0. ) REERDE=ENEERR/ENEENDE
    IF( VOLOUTE /= 0. ) RVERDO=VOLERR/VOLOUTE
    IF( SALOUTE /= 0. ) RSERDO=SALERR/SALOUTE
    IF( DYEOUTE /= 0. ) RDERDO=DYEERR/DYEOUTE
    IF( UMOOUTE /= 0. ) RUMERDO=UMOERR/UMOOUTE
    IF( VMOOUTE /= 0. ) RVMERDO=VMOERR/VMOOUTE
    IF( ENEOUTE /= 0. ) REERDO=ENEERR/ENEOUTE

    ! **  OUTPUT BALANCE RESULTS TO FILE BALE.OUT
    IF( JSBALE == 1 )THEN
      OPEN(89,FILE=OUTDIR//'BALE.OUT',STATUS='UNKNOWN')
      CLOSE(89,STATUS='DELETE')
      OPEN(89,FILE=OUTDIR//'BALE.OUT',STATUS='UNKNOWN')
      JSBALE=0
    ELSE
      OPEN(89,FILE=OUTDIR//'BALE.OUT',POSITION='APPEND',STATUS='UNKNOWN')
    ENDIF
    WRITE(89,890)NTMPD2,N
    WRITE(89,891)
    WRITE(89,892)VOLBEGE,SALBEGE,DYEBEGE,ENEBEGE,UMOBEGE,VMOBEGE,AMOBEGE
    WRITE(89,900)
    WRITE(89,893)
    WRITE(89,892)VOLOUTE,SALOUTE,DYEOUTE,ENEOUTE,UMOOUTE,VMOOUTE
    WRITE(89,900)
    WRITE(89,894)
    WRITE(89,892)VOLBMOE,SALBMOE,DYEBMOE,ENEBMOE,UMOBMOE,VMOBMOE
    WRITE(89,900)
    WRITE(89,895)
    WRITE(89,892)VOLENDE,SALENDE,DYEENDE,ENEENDE,UMOENDE,VMOENDE,AMOENDE
    WRITE(89,900)
    WRITE(89,896)
    WRITE(89,892)VOLERR,SALERR,DYEERR,ENEERR,UMOERR,VMOERR
    WRITE(89,900)
    WRITE(89,897)
    WRITE(89,892)RVERDE,RSERDE,RDERDE,REERDE,RUMERDE,RVMERDE
    WRITE(89,900)
    WRITE(89,898)
    WRITE(89,892)RVERDO,RSERDO,RDERDO,REERDO,RUMERDO,RVMERDO
    WRITE(89,899)
    UUEBMOE=UUEBEGE-UUEOUTE
    VVEBMOE=VVEBEGE-VVEOUTE
    PPEBMOE=PPEBEGE-PPEOUTE
    BBEBMOE=BBEBEGE-BBEOUTE
    WRITE(89,901)UUEBEGE
    WRITE(89,902)UUEOUTE
    WRITE(89,903)UUEBMOE
    WRITE(89,904)UUEENDE
    WRITE(89,900)
    WRITE(89,905)VVEBEGE
    WRITE(89,906)VVEOUTE
    WRITE(89,907)VVEBMOE
    WRITE(89,908)VVEENDE
    WRITE(89,900)
    WRITE(89,909)PPEBEGE
    WRITE(89,910)PPEOUTE
    WRITE(89,911)PPEBMOE
    WRITE(89,912)PPEENDE
    WRITE(89,900)
    WRITE(89,913)BBEBEGE
    WRITE(89,914)BBEOUTE
    WRITE(89,915)BBEBMOE
    WRITE(89,916)BBEENDE
    WRITE(89,900)
    WRITE(89,899)
    CLOSE(89)
    890 FORMAT (' VOLUME, MASS, AND ENERGY BALANCE OVER',I5,' TIME STEPS',' ENDING AT TIME STEP',I5,//)
    891 FORMAT (' INITIAL VOLUME    INITIAL SALT    INITIAL DYE     INITIAL ENER    INITIAL UMO     INITIAL VMO     INITIAL AMO',/)
    892 FORMAT (1X,7(E14.6,2X))
    893 FORMAT (' VOLUME OUT        SALT OUT        DYE OUT         ENERGY OUT      UMO OUT         VMO OUT',/)
    894 FORMAT (' INITIAL-OUT VOL   INIT-OUT SALT   INIT-OUT DYE    INIT-OUT ENER   INIT-OUT UMO    INIT-OUT VMO',/)
    895 FORMAT (' FINAL VOLUME      FINAL SALT      FINAL DYE       FINAL ENERGY    FINAL UMO       FINAL VMO       FINAL AMO',/)
    896 FORMAT (' VOLUME ERR        SALT ERR        DYE ERR         ENERGY ERR      UMO ERR         VMO ERR',/)
    897 FORMAT (' R VOL/END ER      R SAL/END ER    R DYE/END ER    R ENE/END ER    R UMO/END ER    R VMO/END ER',/)
    898 FORMAT (' R VOL/OUT ER      R SAL/OUT ER    R DYE/OUT ER    R ENE/OUT ER    R UMO/OUT ER    R VMO/OUT ER',/)
    899 FORMAT (////)
    900 FORMAT (//)
    901 FORMAT(' UUEBEGE =  ',E14.6)
    902 FORMAT(' UUEOUTE =  ',E14.6)
    903 FORMAT(' UUEBMOE =  ',E14.6)
    904 FORMAT(' UUEENDE =  ',E14.6)
    905 FORMAT(' VVEBEGE =  ',E14.6)
    906 FORMAT(' VVEOUTE =  ',E14.6)
    907 FORMAT(' VVEBMOE =  ',E14.6)
    908 FORMAT(' VVEENDE =  ',E14.6)
    909 FORMAT(' PPEBEGE =  ',E14.6)
    910 FORMAT(' PPEOUTE =  ',E14.6)
    911 FORMAT(' PPEBMOE =  ',E14.6)
    912 FORMAT(' PPEENDE =  ',E14.6)
    913 FORMAT(' BBEBEGE =  ',E14.6)
    914 FORMAT(' BBEOUTE =  ',E14.6)
    915 FORMAT(' BBEBMOE =  ',E14.6)
    916 FORMAT(' BBEENDE =  ',E14.6)
    NBALE=0
  ENDIF
  NBALE=NBALE+1
  RETURN
END

